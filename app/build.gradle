apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.appdistribution'
apply plugin: "com.github.triplet.play"
apply from: "../scripts/versions.gradle"
apply from: "../scripts/secrets.gradle"

android {
    compileSdkVersion rootProject.compileSdkVersion
    defaultConfig {
        applicationId rootProject.applicationId
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode computeVersionCode()
        versionName computeVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "APP_UPDATE_SOURCE_LINK", "\"https://play.google.com/store/apps/details?id=io.novafoundation.nova.market\""
    }
    signingConfigs {
        dev {
            storeFile file('develop_key.jks')
            storePassword readSecretOrNull('CI_KEYSTORE_PASS')
            keyAlias readSecretOrNull('CI_KEYSTORE_KEY_ALIAS')
            keyPassword readSecretOrNull('CI_KEYSTORE_KEY_PASS')
        }
        market {
            storeFile file('market_key.jks')
            storePassword readSecretOrNull('CI_MARKET_KEYSTORE_PASS')
            keyAlias readSecretOrNull('CI_MARKET_KEYSTORE_KEY_ALIAS')
            keyPassword readSecretOrNull('CI_MARKET_KEYSTORE_KEY_PASS')
        }
        github {
            storeFile file('github_key.jks')
            storePassword readSecretOrNull('CI_GITHUB_KEYSTORE_PASS')
            keyAlias readSecretOrNull('CI_GITHUB_KEYSTORE_KEY_ALIAS')
            keyPassword readSecretOrNull('CI_GITHUB_KEYSTORE_KEY_PASS')
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        releaseTest {
            initWith buildTypes.release
            matchingFallbacks = ['release']
            signingConfig signingConfigs.debug

            versionNameSuffix '-releaseTest'
            applicationIdSuffix '.releaseTest'
        }
        releaseMarket {
            initWith buildTypes.release
            matchingFallbacks = ['release']
            signingConfig signingConfigs.market

            versionNameSuffix "-${releaseApplicationSuffix}"
            applicationIdSuffix ".${releaseApplicationSuffix}"
        }
        releaseGithub {
            initWith buildTypes.release
            matchingFallbacks = ['release']
            signingConfig signingConfigs.github

            versionNameSuffix '-github'
            applicationIdSuffix '.github'

            buildConfigField "String", "APP_UPDATE_SOURCE_LINK", "\"https://github.com/nova-wallet/nova-android-app\""
        }
        develop {
            signingConfig signingConfigs.dev
            matchingFallbacks = ['debug']
            versionNameSuffix '-develop'
            applicationIdSuffix '.dev'
            //Init firebase
            def localReleaseNotes = releaseNotes()
            def localFirebaseGroup = firebaseGroup()
            firebaseAppDistribution {
                releaseNotes = localReleaseNotes
                groups = localFirebaseGroup
            }
        }
        instrumentialTest {
            initWith buildTypes.debug
            matchingFallbacks = ['debug']
            defaultConfig.testInstrumentationRunner "io.qameta.allure.android.runners.AllureAndroidJUnitRunner"
        }
    }

    sourceSets {
        releaseGithub {
            res.srcDirs = ['src/release/res']
        }
        releaseMarket {
            res.srcDirs = ['src/release/res']
        }
        releaseTest {
            res.srcDirs = ['src/release/res']
        }
    }

    bundle {
        language {
            enableSplit = false
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    applicationVariants.all { variant ->
        String name = variant.buildType.name
        if (name != "release" && name.startsWith("release")) {
            createBindReleaseFileTask(variant.buildType.name)
        }
    }
}

void createBindReleaseFileTask(String destination) {
    String taskName = "bind${destination.capitalize()}GithubGoogleServicesToRelease"

    Task task = task(taskName, type: Copy) {
        description = "Switches to RELEASE google-services.json for ${destination}"
        from "src/release"
        include "google-services.json"
        into "src/${destination}"
    }

    afterEvaluate {
        tasks.getByName("process${destination.capitalize()}GoogleServices").dependsOn task
    }
}

play {
    serviceAccountCredentials = file(System.env.CI_PLAY_KEY ?: "../key/fake.json")
    track = "beta"
    releaseStatus = "completed"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':core-db')
    implementation project(':common')
    implementation project(':feature-splash')

    implementation project(':feature-onboarding-api')
    implementation project(':feature-onboarding-impl')

    implementation project(':feature-ledger-api')
    implementation project(':feature-ledger-impl')

    implementation project(':feature-account-api')
    implementation project(':feature-account-impl')

    implementation project(':feature-wallet-api')
    implementation project(':feature-wallet-impl')

    implementation project(':runtime')

    implementation project(':feature-staking-api')
    implementation project(':feature-staking-impl')

    implementation project(':feature-crowdloan-api')
    implementation project(':feature-crowdloan-impl')

    implementation project(':feature-dapp-api')
    implementation project(':feature-dapp-impl')

    implementation project(':feature-nft-api')
    implementation project(':feature-nft-impl')

    implementation project(':feature-currency-api')
    implementation project(':feature-currency-impl')

    implementation project(':feature-governance-api')
    implementation project(':feature-governance-impl')

    implementation project(':feature-assets')

    implementation project(':feature-vote')

    implementation project(':feature-versions-api')
    implementation project(':feature-versions-impl')

    implementation kotlinDep

    implementation androidDep
    implementation constraintDep

    implementation zXingEmbeddedDep

    implementation navigationFragmentDep
    implementation navigationUiDep

    implementation roomDep

    implementation fearlessLibDep

    implementation daggerDep
    kapt daggerKapt

    implementation lifecycleDep
    kapt lifecycleKapt

    implementation lifeCycleKtxDep

    implementation retrofitDep
    implementation gsonConvertedDep

    implementation gifDep

    compileOnly wsDep

    implementation coroutinesDep

    testImplementation project(':test-shared')

    implementation insetterDep

    implementation liveDataKtxDep

    kaptAndroidTest daggerKapt

    androidTestImplementation androidTestRunnerDep
    androidTestImplementation androidTestRulesDep
    androidTestImplementation androidJunitDep

    androidTestImplementation allureKotlinModel
    androidTestImplementation allureKotlinCommons
    androidTestImplementation allureKotlinJunit4
    androidTestImplementation allureKotlinAndroid
}

task printVersion {
    doLast {
        println "versionName:${computeVersionName()}"
    }
}
